dnl Copyright (C) 1999-2001, Kelly Fitz and Lippold Haken <loris@cerlsoundgroup.org>
dnl  
dnl This file is free software; as a special exception the author gives
dnl unlimited permission to copy and/or distribute it, with or without 
dnl modifications, as long as this notice is preserved.
dnl 
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY, to the extent permitted by law; without even the
dnl implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.


dnl initialization
AC_INIT(Loris, 1.0.3, loris@cerlsoundgroup.org, loris)

AC_CONFIG_AUX_DIR(config)
AC_SUBST(ac_aux_dir)
AM_INIT_AUTOMAKE(1.6)
AM_CONFIG_HEADER(config.h)

dnl----------------------------------------------------------------
dnl Package Name And Version
dnl----------------------------------------------------------------

AC_MSG_RESULT(----- Package Name And Version -----)

AC_MSG_RESULT(this is $PACKAGE_STRING)

dnl Create loris.h from loris.h.in, subsituting the 
dnl values of the version macros.
dnl Make sure these are consistent with the version number
dnl in AC_INIT above!
LORIS_MAJOR_VERSION=1
LORIS_MINOR_VERSION=0
LORIS_SUBMINOR_VERSION=3
LORIS_PRERELEASE_STR=
LORIS_VERSION_STR="$PACKAGE_STRING$LORIS_PRERELEASE_STR"
AC_SUBST(LORIS_MAJOR_VERSION)
AC_SUBST(LORIS_MINOR_VERSION)
AC_SUBST(LORIS_SUBMINOR_VERSION)
AC_SUBST(LORIS_VERSION_STR)
AC_CONFIG_FILES([pi/loris.h])


dnl----------------------------------------------------------------
dnl Checks for programs.
dnl----------------------------------------------------------------

AC_MSG_RESULT(----- Program Checks -----)
AC_PROG_CXX
AM_PROG_LIBTOOL

dnl----------------------------------------------------------------
dnl Perform special libtool checks under Darwin.
dnl----------------------------------------------------------------

AC_DEFUN([PATCH_LIBTOOL_ON_DARWIN_ZSH_OVERQUOTING],
[# libtool-1.4 specific, on zsh target the final requoting does one too much
case "$host_os" in
  darwin*)
    if grep "1.920" libtool ; then
AC_MSG_RESULT(patching libtool on .so-sharedlib creation (zsh overquoting))
      test -f libtool.old || (mv libtool libtool.old && cp libtool.old libtool)
      sed -e '/archive_cmds=/s:\\\\":\\":g' libtool > libtool.new
      mv libtool.new libtool
      test -f libtool || (test -f libtool.old && mv libtool.old libtool)
    fi
  ;;
esac
])
PATCH_LIBTOOL_ON_DARWIN_ZSH_OVERQUOTING

AC_DEFUN([KELLYS_GREAT_DARWIN_FIX],
[# libtool just won't build the scripting modules under Darwin, do it by hand
case "$host_os" in
  *darwin*)
	AC_MSG_RESULT(Using Darwin-specific kludges to avoid broken libtool.)
	DARWIN=true
	;;
esac
])

KELLYS_GREAT_DARWIN_FIX
AM_CONDITIONAL(DARWIN, test "$DARWIN")

dnl----------------------------------------------------------------
dnl Look for swig.
dnl
dnl If swig is available, then a target will be generated 
dnl for rebuilding the scripting wrappers.
dnl----------------------------------------------------------------

AC_PATH_PROG(SWIG, swig)
AM_CONDITIONAL(HAVE_SWIG, test -n "$SWIG")

dnl----------------------------------------------------------------
dnl Look for csound.
dnl
dnl If csound is available, then a message will be printed
dnl refering to the README in the csound directory.
dnl----------------------------------------------------------------

AC_PATH_PROG(CSOUND, csound)
AM_CONDITIONAL(HAVE_CSOUND, test -n "$CSOUND")

if test -n "$HAVE_CSOUND"; then
	AC_MSG_RESULT(-----------------------------------------)
	AC_MSG_RESULT(You have Csound installed. See the)
	AC_MSG_RESULT(README file in the csound directory)
	AC_MSG_RESULT(for information about building the)
	AC_MSG_RESULT(Loris csound modules.)
	AC_MSG_RESULT(-----------------------------------------)
fi

dnl----------------------------------------------------------------
dnl Checks for compiler features.

AC_MSG_RESULT(----- Compiler Feature Checks -----)
AC_LANG_SAVE
AC_LANG_CPLUSPLUS

dnl SIZEOF buildin integer types, min is the minmimum according ISO
dnl SIZEOF_BOOL
AC_CHECK_SIZEOF(bool)
dnl SIZEOF_CHAR, min = 1
AC_CHECK_SIZEOF(char)
dnl define __CHAR_UNSIGNED__ , unless defined by compiler
AC_C_CHAR_UNSIGNED
dnl SIZEOF_SHORT, min = 2
AC_CHECK_SIZEOF(short)
dnl SIZEOF_INT, min = 4
AC_CHECK_SIZEOF(int)
dnl SIZEOF_LONG, min = 4
AC_CHECK_SIZEOF(long)
dnl assign default, if not defined
dnl AC_CHECK_TYPE([long long], long)
dnl AC_CHECK_TYPE([unsigned long long], [unsigned long])
dnl SIZEOF_LONG_LONG, 0 if undefined
dnl AC_CHECK_SIZEOF([long long])
dnl SIZEOF_INT_P
AC_CHECK_SIZEOF(int *) 
dnl SIZEOF_SIZE_T
AC_CHECK_SIZEOF(size_t) 

dnl SIZEOF_FLOAT, should always be 4 I think
AC_CHECK_SIZEOF(float)
dnl SIZEOF_DOUBLE, should always be 8 I think
AC_CHECK_SIZEOF(double)

dnl Check for a definition of M_PI in cmath, if not use our own.
AH_TEMPLATE([HAVE_M_PI],
            [Define 1 if M_PI defined in cmath, 0 otherwise.])
AC_MSG_CHECKING(for M_PI defined in cmath)
AC_TRY_COMPILE([ #include <cmath>;], [double pi = M_PI;], [
	AC_MSG_RESULT(yep)
	AC_DEFINE([HAVE_M_PI], 1) ], [
	AC_MSG_RESULT(nope using local definition of pi)
	AC_DEFINE([HAVE_M_PI], 0) ] )


dnl check whether template member functions should
dnl be used
AH_TEMPLATE([NO_TEMPLATE_MEMBERS],
            [Define for compilers that do not support template member functions.])
AC_MSG_CHECKING(whether C++ compiler supports template member functions)
AC_COMPILE_IFELSE([
	AC_LANG_SOURCE([[
		#include <vector>
		struct C
		{
			int z;
			template< class T >
			C( T b, T e ) : z( e-b ) {}
		};]], [[
		std::vector< int > v(100, 1);
		C c( v.begin(), v .end() );
		return 0;
		]])], [
	AC_MSG_RESULT(yes)], [
	AC_MSG_RESULT(no, not using template members)
	AC_DEFINE([NO_TEMPLATE_MEMBERS])])

dnl check whether the compiler can handle Alexandrescu's compile-time
dnl assertions, which we use later to make sure that std::complex<double>
dnl and fftw_complex are identical:
AH_TEMPLATE([NO_COMPILE_TIME_ASSERTIONS],
            [Define for compilers that do not support compile-time assertions.])
AC_MSG_CHECKING(whether compiler supports compile-time assertions)

AC_COMPILE_IFELSE( 
	AC_LANG_SOURCE([[
		//	Gnarly code adopted from Alexandrescu (Modern C++ Design) for compile-time
		//	checking of type information. 
		template<int> struct CompileTimeError;
		template<> struct CompileTimeError<true> {};
		#define STATIC_CHECK(expr, msg) \
			{ CompileTimeError<((expr) != 0)> ERROR_##msg; (void)ERROR_##msg; }
		
		template <class T, class U> struct CompareTypes { enum { same = 0 }; };
		template<class T> struct CompareTypes<T, T> { enum { same = 1 }; };
		typedef int INT;
		void f(void)
		{
			STATIC_CHECK((CompareTypes<int, INT>::same!=0), COMPILE_TIME_ASSERTIONS_FAIL);
			STATIC_CHECK((CompareTypes<int, float>::same==0), COMPILE_TIME_ASSERTIONS_FAIL);
		}
  ]]),[
  NO_COMPILE_TIME_ASSERTIONS=false
  AC_MSG_RESULT(yes) ], [
  NO_COMPILE_TIME_ASSERTIONS=true
  AC_MSG_RESULT(no)
  AC_DEFINE([NO_COMPILE_TIME_ASSERTIONS])
  ] )

dnl restore this
AC_LANG_RESTORE

dnl----------------------------------------------------------------
dnl Check for librairies
dnl----------------------------------------------------------------

AC_MSG_RESULT(----- Library Checks -----)

dnl----------------------------------------------------------------
dnl Look for IRCAM's SDIF package
dnl
dnl Allow an argument to specify the prefix of a non-installed
dnl SDIF package, since there are more than one such package in
dnl and we need specifically IRCAM's.
dnl----------------------------------------------------------------

SDIFPACKAGE=
SDIFINCLUDE=
 
AC_ARG_WITH(sdif,
	AC_HELP_STRING( [--with-sdif=path],
					[specify the prefix of the SDIF package] ),
	[SDIFPACKAGE="$withval"], [SDIFPACKAGE=])

if test -n "$SDIFPACKAGE" ; then
	AC_MSG_RESULT([prepending $SDIFPACKAGE/include to include search path and $SDIFPACKAGE/lib to library search path (sdif package)])
	SDIFINCLUDE="$SDIFPACKAGE/include"
	CPPFLAGS="-I$SDIFINCLUDE $CPPFLAGS"
	LIBS="-L$SDIFPACKAGE/lib $LIBS"
fi

AC_CHECK_HEADER(sdif.h, :, AC_MSG_ERROR(cannot find SDIF header))
AC_CHECK_LIB(sdif, SdifGenInit, :, AC_MSG_ERROR(cannot use SDIF library))

dnl make sure that we can figure out how to use
dnl the SDIF library
AC_MSG_CHECKING(SDIF release version (must be at least 3.4))
dnl should actually try running this, but these things weren't defined
dnl in earlier versions, so probably it isn't worth the trouble
AC_TRY_COMPILE( [ #include<sdif.h> ], [
		assert( SDIF_VERSION_MAJOR >= 3 && SDIF_VERSION_MINOR >= 4 ); ],[ 
		AC_MSG_RESULT(ok)
		], [
		AC_MSG_ERROR(failed -- please use at least SDIF-3.4) ] )

if test -n "$SDIFINCLUDE" ; then
	SDIFINCLUDE="-I$SDIFINCLUDE"
fi
AC_SUBST(SDIFINCLUDE)

dnl
dnl end of SDIF check
dnl----------------------------------------------------------------


dnl----------------------------------------------------------------
dnl Look for FFTW
dnl
dnl Remember that fftw library won't link without -lm.
dnl Allow an argument to specify the prefix of a non-installed
dnl FFTW package
dnl----------------------------------------------------------------

FFTWPACKAGE=
FFTWINCLUDE=

AC_ARG_WITH(fftw,
	AC_HELP_STRING( [--with-fftw=path],
					[specify the prefix of the FFTW  package] ),
	[FFTWPACKAGE="$withval"], [FFTWPACKAGE=])

if test -n "$FFTWPACKAGE" ; then
	AC_MSG_RESULT([prepending $FFTWPACKAGE/include to include search path and $FFTWPACKAGE/lib to library search path (fftw package)])
	FFTWINCLUDE="$FFTWPACKAGE/include"
	CPPFLAGS="-I$FFTWINCLUDE $CPPFLAGS"
	LIBS="-L$FFTWPACKAGE/lib $LIBS"
fi

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

AC_CHECK_HEADER(fftw.h, :, AC_MSG_ERROR(cannot find FFTW header))
AC_CHECK_LIB(fftw, fftw_create_plan, :, AC_MSG_ERROR(cannot use FFTW, library), -lm)

if test -n "$FFTWINCLUDE" ; then
	FFTWINCLUDE="-I$FFTWINCLUDE"
fi
AC_SUBST(FFTWINCLUDE)

dnl Gnarly code adopted from Alexandrescu (Modern C++ Design) for compile-time
dnl checking of type information. Need to make sure that fftw_complex and 
dnl std::complex<double> are the same size and have the same memory layout.
dnl Try to do that at compile-time, much better than doing it at runtime.
dnl Actually, best of all is at configure-time. Only do this if the compiler
dnl can handle compile-time assertions (checked above).
if test -n "$NO_COMPILE_TIME_ASSERTIONS"; then
	dnl compiler supports compile-time assertions
	AC_MSG_CHECKING(whether std::complex<double> is same as fftw_complex)
	AC_COMPILE_IFELSE( 
		AC_LANG_SOURCE([[
			#include <complex>
			#include <fftw.h>
			//	Gnarly code adopted from Alexandrescu (Modern C++ Design) for compile-time
			//	checking of type information. Need to make sure that fftw_complex and 
			//	std::complex<double> are the same size and have the same memory layout.
			//
			template<int> struct CompileTimeError;
			template<> struct CompileTimeError<true> {};
			#define STATIC_CHECK(expr, msg) \
				{ CompileTimeError<((expr) != 0)> ERROR_##msg; (void)ERROR_##msg; }
			
			template <class T, class U> struct CompareTypes { enum { same = 0 }; };
			template<class T> struct CompareTypes<T, T> { enum { same = 1 }; };
			
			void f(void)
			{
				//	if this won't compile, fftw_real is not defined to be double,
				//	and this class won't work under those conditions:
				STATIC_CHECK((CompareTypes<double, fftw_real>::same!=0), fftw_real_IS_NOT_DOUBLE);
			
				//	if this won't compile, then the two complex types have 
				//	different sizes, and therefore don't have the same memory 
				//	layout, and this FourierTransform class won't work:
				STATIC_CHECK( sizeof(std::complex<double>) == sizeof(fftw_complex), 
							  fftw_complex_DIFFERENT_SIZE_FROM_std_complex_double );
							  
				//	if this doesn't compile, then probably the previous one didn't
				//	compile either, but it doesn't hurt to verify that the layout
				//	of the complex types is trivial:
				STATIC_CHECK( sizeof(fftw_complex) == sizeof(double) + sizeof(double),
							  fftw_complex_LAYOUT_IS_NONTRIVIAL );
			}
	  ]]),[
	  AC_MSG_RESULT(seems so) ], [
	  AC_MSG_RESULT(failed!)
	  AC_MSG_RESULT(-------------------------------------------)
	  AC_MSG_RESULT(Compile-time type checks failed!!!!)
	  AC_MSG_RESULT(This suggests that std::complex<double> and)
	  AC_MSG_RESULT(and fftw_complex do not have the same)
	  AC_MSG_RESULT(size and memory layout, so Loris analysis)
	  AC_MSG_RESULT(will not work!!!)
	  AC_MSG_RESULT(Check config.log, and send email to)
	  AC_MSG_RESULT($PACKAGE_BUGREPORT)
	  AC_MSG_RESULT(if you think this message is an error.)
	  AC_MSG_RESULT(-------------------------------------------)
	  AC_MSG_ERROR(Compile-time type checks failed!!!!)
	  ] )
else
	AC_MSG_RESULT(cannot use compile-time checks to compare std::complex<double> to fftw_complex)
fi


dnl restore this
AC_LANG_RESTORE

dnl
dnl end of FFTW check
dnl----------------------------------------------------------------

dnl----------------------------------------------------------------
dnl Check for scripting languages
dnl----------------------------------------------------------------

AC_MSG_RESULT(----- Language Checks -----)

dnl----------------------------------------------------------------
dnl Look for Python
dnl----------------------------------------------------------------
dnl By default, look for a python binary, and use the header in the
dnl same path. If --with-python is specified, then if the argument is
dnl a file, set the PYTHON environment variable to that value before
dnl searching, otherwise, if the argument is a directory, assume that
dnl it is the prefix for the python installation, and add /bin/python
dnl to it to set the PYTHON environment variable. If --without-python
dnl or --with-python=no is specified, don't use python at all.

AC_ARG_WITH(python,
	AC_HELP_STRING( [--with-python@<:@=path@:>@],
					[build Python module (default is YES) (optionally specify prefix of Python package)] ),
	[USEPY="$withval"], [USEPY="yes"])

PYINCLUDE=
AC_ARG_WITH(pyincl,
	AC_HELP_STRING( [--with-pyincl=path],
					[specify path of the Python.h header] ),
	[PYINCLUDE="$withval"], [PYINCLUDE=])

if test  "$USEPY" != "no"; then
	if test -f "$USEPY"; then
		dnl assume that the python binary was specified
		PYTHON="$USEPY"
	fi
	if test -d "$USEPY"; then
		dnl assume that the python prefix was specified
		PYTHON="$USEPY/bin/python"
	fi
	dnl use automake tools for finding python, set 
	dnl installation directory for extension modules
	AM_PATH_PYTHON(1.5)
	AC_MSG_RESULT(installing Python extension modules in $pyexecdir)
	
	dnl look for the header Python.h
	AC_MSG_CHECKING(for Python.h header file)
	if test -n "$PYINCLUDE" ; then 
		dnl if the header path has been specified, verify 
		dnl that it exists and is a directory
		if test -f "$PYINCLUDE" ; then 
			dnl remove the filename
			PYINCLUDE="`AS_DIRNAME(["$PYINCLUDE"])`"
		fi
		if test ! -f "$PYINCLUDE/Python.h" ; then
			AC_MSG_ERROR(cannot find Python.h in $PYINCLUDE)
		fi
	else
		dnl if the header path was not specified, try
		dnl to infer it from the python binary
		PYINCLUDE="`AS_DIRNAME(["$PYTHON"])`/../include/python$PYTHON_VERSION"  
		if test ! -f "$PYINCLUDE/Python.h" ; then
			AC_MSG_ERROR(cannot find Python.h in $PYINCLUDE please use --with-pyincl=path)
		fi
	fi
	AC_MSG_RESULT(found in $PYINCLUDE)
	
	dnl check whether this version of Python supports
	dnl the new style of iterators
	AC_MSG_CHECKING(for PyExc_StopIteration defined in Python.h)
	AC_TRY_COMPILE(	[ #include <$PYINCLUDE/Python.h>;], 
					[ PyErr_SetString( PyExc_StopIteration, ""); ], 
		[
	    	AC_MSG_RESULT(yep)
			PYINCLUDE="-I$PYINCLUDE"	
	    ], [
	    	AC_MSG_RESULT(nope)
			PYINCLUDE="-I$PYINCLUDE -DNO_PYTHON_EXC_STOPITER"
	    ] )
fi

AM_CONDITIONAL(BUILD_PYTHON, test "$USEPY" != "no")
AC_SUBST(PYINCLUDE)


dnl--------------------------------------------------------------------
dnl	Try to locate the Tcl package
dnl--------------------------------------------------------------------
dnl By default, look for a tclsh binary, and use the header in the same
dnl path. If --with-tcl is specified, then if the argument is
dnl a file, look first in the directory cotnaining that file, otherwise
dnl if the argument is a directory, assume that it is the prefix for the 
dnl tcl installation, and look first in that directory/bin. If --without-tcl
dnl or --with-tcl=no is specified, don't use tcl at all.

AC_ARG_WITH(tcl,
	AC_HELP_STRING( [--with-tcl@<:@=path@:>@],
					[build Tcl module (default is YES) (optionally specify prefix of Tcl package)] ),
	[USETCL="$withval"], [USETCL="yes"])
	
TCLINCLUDE=
AC_ARG_WITH(tclincl,
	AC_HELP_STRING( [--with-tclincl=path],
					[specify path of the tcl.h header] ),
	[TCLINCLUDE="$withval"], [TCLINCLUDE=])
	
if test  "$USETCL" != "no"; then
	if test -f "$USETCL"; then
		dnl assume that the tclsh binary was specified
		TCLPREFIX="`AS_DIRNAME(["$USETCL"])`/.."
	fi
	if test -d "$USETCL"; then
		dnl assume that the tclsh prefix was specified
		TCLPREFIX="$USETCL"
	fi
	dnl if TCLPREFIX still has not been set, then try to
	dnl use automake tools for finding a program (tclsh)
	AC_PATH_PROG(TCLSH, tclsh, [], [$TCLPREFIX/bin$PATH_SEPARATOR$PATH])
	if test -z "$TCLSH"; then
		AC_MSG_ERROR(cannot find tclsh in $TCLPREFIX/bin$PATH_SEPARATOR$PATH)
	fi
		
	dnl look for the header tcl.h
	AC_MSG_CHECKING(for tcl.h header file)
	if test -n "$TCLINCLUDE" ; then 
		dnl if the header path has been specified, verify 
		dnl that it exists and is a directory
		if test -f "$TCLINCLUDE" ; then 
			dnl remove the filename
			TCLINCLUDE="`AS_DIRNAME(["$TCLINCLUDE"])`"
		fi
		if test ! -f "$TCLINCLUDE/tcl.h" ; then
			AC_MSG_ERROR(cannot find tcl.h in $TCLINCLUDE)
		fi
	else
		dnl if the header path was not specified, try
		dnl to infer it from the tclsh binary
		TCLINCLUDE="`AS_DIRNAME(["$TCLSH"])`/../include"  
		if test ! -f "$TCLINCLUDE/tcl.h" ; then
			AC_MSG_ERROR(cannot find tcl.h in $TCLINCLUDE please use --with-tclincl=path)
		fi
	fi
	AC_MSG_RESULT(found in $TCLINCLUDE)
	TCLINCLUDE="-I$TCLINCLUDE"
fi

AM_CONDITIONAL(BUILD_TCL, test "$USETCL" != "no")
AC_SUBST(TCLINCLUDE)

dnl--------------------------------------------------------------------
dnl	Try to locate Perl
dnl--------------------------------------------------------------------
dnl By default, do not build the Perl module, because it has never been
dnl tested. If the user insists, try to locate Perl and the necessary 
dnl headers. If --with-perl is specified, then if the argument is
dnl a file, assume that it is the perl binary and use it, otherwise
dnl if the argument is a directory, assume that it is the prefix for the 
dnl Perl installation, and look first in that directory/bin.

AC_ARG_WITH(perl,
	AC_HELP_STRING( [--with-perl@<:@=path@:>@],
					[build Perl module (default is NO) (optionally specify prefix of Perl package)] ),
	[USEPERL="$withval"], [USEPERL="no"])
	
PERLINCLUDE=
if test  "$USEPERL" != "no"; then
	if test -f "$USEPERL"; then
		dnl assume that the perl binary was specified
		PERL="$USEPERL"
	fi
	if test -d "$USEPERL"; then
		dnl assume that the perl prefix was specified,
		dnl use automake tools for finding a program (perl)
		PERLPREFIX="$USEPERL"
		AC_PATH_PROG(PERL, perl, [], [$PERLPREFIX/bin])
		if test -z "$PERL"; then
			AC_MSG_ERROR(cannot find perl in $PERLPREFIX/bin$PATH_SEPARATOR$PATH)
		fi
	fi
	if test -z "$PERL"; then
		if test "$USEPERL" != "yes"; then
			AC_MSG_WARN(ignoring $USEPERL neither file nor directory)
		fi
		AC_PATH_PROG(PERL, perl)
		if test -z "$PERL"; then
			AC_MSG_ERROR(cannot find perl in $PATH)
		fi
	fi	
	
	dnl use Perl to identify the directory with the headers
	AC_MSG_CHECKING(for perl5 headers directory)
	PERLINCLUDE=`($PERL -e 'use Config; print $Config{archlib};') 2>/dev/null`/CORE
	if test ! -d "$PERLINCLUDE"; then
		AC_MSG_ERROR(cannot find perl5 headers directory $PERLINCLUDE)
	fi
	AC_MSG_RESULT(found $PERLINCLUDE)
	PERLINCLUDE="-I$PERLINCLUDE"

	dnl construct the site-perl installation path
	VER=`($PERL -e 'use Config; print $Config{version};') 2>/dev/null`
	ARCH=`($PERL -e 'use Config; print $Config{archname};') 2>/dev/null`
	SITEPERL="perl5/site_perl/$VER/$ARCH"
	AC_MSG_RESULT(installing Perl5 extension modules in \${exec_prefix}/lib/$SITEPERL)
fi

AM_CONDITIONAL(BUILD_PERL, test "$USEPERL" != "no")
AC_SUBST(PERLINCLUDE)
AC_SUBST(SITEPERL)

dnl----------------------------------------------------------------
dnl Check for Loris debugging flag
dnl----------------------------------------------------------------
AC_ARG_ENABLE(debugloris,
	AC_HELP_STRING( [--enable-debugloris],
					[enable internal Loris debugging code (not recommended)] ),
	[DEBUG_LORIS="$enableval" ], [DEBUG_LORIS=] )

AH_TEMPLATE([Debug_Loris],
            [Define to enable internal Loris debugging code (not recommended).])

if test -n "$DEBUG_LORIS"; then
	if test "$DEBUG_LORIS"="yes"; then
		AC_MSG_RESULT(enabling debugging code (defining Debug_Loris))
		AC_DEFINE([Debug_Loris])
	else
		AC_MSG_RESULT(enabling debugging code (defining Debug_Loris to $DEBUG_LORIS))
		AC_DEFINE([Debug_Loris], "$DEBUG_LORIS")
	fi
fi

dnl----------------------------------------------------------------
dnl Generate Makefiles
dnl----------------------------------------------------------------

AC_MSG_RESULT(----- Creating Output Files -----)

AC_OUTPUT(Makefile doc/Makefile src/Makefile \
pi/Makefile scripting/Makefile csound/Makefile test/Makefile )

AC_MSG_RESULT(----- End Of Configuration -----)
